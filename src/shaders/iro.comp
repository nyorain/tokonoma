#version 450

#extension GL_GOOGLE_include_directive : enable

#include "iro.glsl"

// invariants (before/after every exeuction):
// - fields.player == playerNone <=>
// 		(field.type == FieldEmpty && field.strength = 0.f);
// - fields.player != playerNone => (field.player < playerCount);

const uint playerCount = 2u; // TODO: only 2 works atm, see below

// the amount of diffuse per difference in every step
// if one cell has strength 1 and its neighbor 0, so much will float
// into the neighbor. If two neighbor cells don't have the same
// player, the stronger one will gain the cell
// must be in range [0, 1/6]
const float diffuse = 0.01;
const float diffuseVel = 0.05;

// TODO: not used, just a concept atm. Work out clash model further
// we probably first want to track how many fighters from each player
// are there (from *all* neighbors) and then evaulate the fight and losses
// using this factor
//
// by how much if matters in a fight which side has more forces.
// must be in range [1, inf)
// if this is 1, the result of a fight will only be the net difference
// of fighting forces in both cells. If this goes against infinity,
// the stronger side will have less losses and the stronger side more.
const float advantageInfluence = 2;

// constant tower damage per tick
const float towerDamage = 0.01;

// constant strength spawned by building
const float spawnStrength = 0.001;

// influence factor of accelerator field
const float accelInfluence = 0.05;

// Information about a player
struct Player {
	uint resources; // should only be atomically accessed
	float _1, _2, _3; // padding
};

// TODO: test different settings for performance
// performance would probably also be improved if we use 2D groups
//   but then group the ids matchingly (so that we always execute
//   blocks close to each other). Since they access roughly the
//   same memory.
layout(local_size_x = 1) in;

// the old buffer. All reads must be performed from this since the other
// buffer may be written by another execution. Holds that was written
// the last step
layout(set = 0, binding = 0, std430) readonly buffer OldFields {
	Field[] fields;
} old;

// must only be written since it values are otherwise undefined.
// the final state should be written into this
layout(set = 0, binding = 1, std430) writeonly buffer NewFields {
	Field[] fields;
} new;

// common data about the players
layout(set = 0, binding = 2, std430) buffer Players {
	Player[playerCount] players;
} players;

float diffuseFac(vec2 vel, vec2 dir) {
	float fac = mix(0.5 * oneSixth, 500 * dot(dir, vel), 500 * dot(vel, vel));
	return clamp(fac, 0.0, 1.0);
}

void main() {
	uint id = gl_GlobalInvocationID.x;
	Field field = old.fields[id];

	uint player = field.player;

	// NOTE: something like pressure?
	// fields that have more strength should (not just proportionally)
	// diffuse "stronger"

	// NOTE: fix nan velocities

	// diffuse
	// 1: depart/general
	float strength = field.strength;
	vec2 vel = field.velocity;
	// float l = length(vel);
	uint freeOwn = 0;
	for(uint i = 0; i < 6; ++i) {
		if(field.next[i] == none) {
			// edge condition
			// TODO: also apply this for buildings?
			if(field.type == FieldEmpty) {
				// TODO: factor:
				// 1.0: erase all velocity in that direction
				// 2.0: full bounce back
				float d = max(dot(field.velocity, direction(i)), 0);
				field.velocity -= 0.8 * d * direction(i);
			}

			continue;
		}


		const Field next = old.fields[field.next[i]];
		// float fac = clamp(mix(oneSixth, 10 * dot(direction(i), vel), tanh(20 * l)), 0.0, 1.0);
		float fac = diffuseFac(vel, direction(i));

		if(next.type == FieldEmpty) {
			if(next.player == field.player) {
				++freeOwn;
			}

			if(field.type == FieldEmpty) {
				// pressure/even out
				// TODO: probably better solution for this...
				if(strength > 1.0 && strength > next.strength) {
					// field.velocity += 0.0001 * direction(i) * (strength - next.strength);

					// slowly die
					field.strength -= 0.05 * (strength - 1.0);
				}

				// TODO: better diffusion?
				// maybe don't change velocity, just make diffusion
				// stronger over boundaries with huge difference?
				float d = strength - next.strength;
				field.velocity += 0.001 * direction(i) * d * d * d;

				// attack/diffuse: field -> next
				field.strength -= fac * diffuse * strength;
				field.velocity -= fac * diffuseVel * strength * vel;
			}
		}
	}

	// resources
	if(field.type == FieldResource) {
		atomicAdd(players.players[field.player].resources, freeOwn);
	}

	// XXX: slow down over time?
	field.velocity *= 0.999;

	// 2: receive
	float strengths[playerCount] = {0.0, 0.0}; // player strengths in this field
	vec2 velSum = vec2(0.0, 0.0); // weighted total velocity sum
	for(uint i = 0; i < 6; ++i) {
		if(field.next[i] == none) {
			continue;
		}

		const Field next = old.fields[field.next[i]];
		// float l = length(next.velocity);
		// float fac = clamp(mix(oneSixth, 10 * dot(-direction(i), next.velocity), tanh(20 * l)), 0.0, 1.0);

		float fac = diffuseFac(next.velocity, -direction(i));

		// TODO: strength cannot be greater than 1? normalize somehow everywhere!
		// can't move into friendly building, only to attack
		if(next.type == FieldEmpty && (field.type == FieldEmpty || next.player != field.player)) {
			// diffuse (friendly or attack): next -> field
			strengths[next.player] += fac * diffuse * next.strength;

			// TODO: should enemies influence acceleration building vel?
			if(field.type == FieldEmpty) {
				velSum += fac * diffuseVel * next.strength * next.velocity;
			}
		} else if(next.type == FieldTower && next.player != field.player) {
			field.strength -= towerDamage;
		} else if(next.type == FieldSpawn && strengths[next.player] < 1.f) {
			// spawn without any velocity
			strengths[next.player] += spawnStrength;
		} else if(next.type == FieldAccel && next.player == field.player) {
			// acceleration fields can't influence one another.
			// that leads to divergence
			if(field.type != FieldAccel) {
				velSum += field.strength * accelInfluence * next.velocity; // accelerate
			}
		}
	}

	// XXX: should never be below 0 anyways?
	field.strength = max(0.0, field.strength);
	if(field.player != none) {
		strengths[field.player] += field.strength;
		velSum += field.velocity;
	}

	// 3: fight
	uint bestPlayer = none;
	float highestStrength = 0.0;
	float sum = 0.0; // TODO: simple sum doesn't work for playerCount > 2
	for(uint i = 0; i < playerCount; ++i) {
		sum += strengths[i];
		if(strengths[i] > highestStrength) {
			bestPlayer = i;
			highestStrength = strengths[i];
		}
	}

	// change field ownership/destroy building
	if(bestPlayer != field.player) {
		field.type = FieldEmpty; // destroy building if one is left
		// velSum = vec2(0.0, 0.0); // no velocity after invasion
	}

	// TODO: when force is lost, velocity should be lost as well
	field.strength = 2 * highestStrength - sum; // remainder
	field.player = bestPlayer;
	if(bestPlayer != none) {
		// field.velocity = clamp(velSum, -1, 1);
		// TODO
		float l = max(length(velSum), 1.0);
		field.velocity = velSum / l;
	}

	// write back
	new.fields[id] = field;
}
